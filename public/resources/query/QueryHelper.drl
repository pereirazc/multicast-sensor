package query;

import models.*;

query "UserByMailAndPassword" (String mail, String pswd)
   user: User(email==mail, password==pswd)
end

query "UserByMail" (String mail)
   user: User(email==mail)
end

query "UserByToken" (String token)
   auth: Auth(this.token==token, user: this.user)
end

query "Sensors" (long userId)
    user: User(this.userId == userId)
    sensor: Sensor(owner == user)
end

query "DeviceById" (long userId, String deviceId)
    user: User(this.userId == userId)
    device: Device(this.deviceId == deviceId, owner == user)
end

query "NotificationsByUser" (long userId, String deviceId)
    $user: User(this.userId == userId)
    $device: Device(this.deviceId == deviceId, owner == $user)
    $sensor: Sensor(owner == $user)
    $feed: Feed(this.sensor == $sensor)
    notification: Notification(data.feed == $feed)
    not Delivered(this.notification==notification, device == $device)
end

query "NotificationById" (long userId, long notificationId)
    $user: User(this.userId == userId)
    $sensor: Sensor(owner == $user)
    $feed: Feed(this.sensor == $sensor)
    notification: Notification(id == notificationId, data.feed == $feed)
end

query "Sensor" (long userId, String sensorId)
    user: User(this.userId == userId)
    sensor: Sensor(owner == user, this.sensorId==sensorId)
end

query "Feeds" (long userId, String sensorId)
    user: User(this.userId == userId)
    sensor: Sensor(owner == user, this.sensorId==sensorId)
    feed: Feed(this.sensor == sensor)
end

query "Feed" (String sensorId, String feedId)
    sensor: Sensor(this.sensorId==sensorId)
    feed: Feed(this.sensor == sensor, this.feedId==feedId)
end

query "FeedByUser" (long userId, String sensorId, String feedId)
    user: User(this.userId == userId)
    sensor: Sensor(owner == user, this.sensorId==sensorId)
    feed: Feed(this.sensor == sensor, this.feedId==feedId)
end

query "FeedStream" (long userId, String sensorId, String feedId)
    user: User(this.userId == userId)
    sensor: Sensor(this.sensorId==sensorId)
    feed: Feed(this.sensor == sensor, this.feedId== feedId)
    accumulate($e: Data(this.feed == feed) over window:length(30),
                stream: collectList( $e ))
end

//=============================

query "Devices"
    device: Device()
end

query "DeliveredNotifications"
    delivered: Delivered()
end

query "AllNotifications" (long threshold)
    notification: Notification(id > threshold)
end

